// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Amount of each material to complete dissipate radiation
#define WOOD_FULL_AMT 4.7
#define IRON_FULL_AMT 1
#define CONCRETE_FULL_AMT 2
#define LEAD_FULL_AMT 0.45

// Axis Aligned Bounding Box of an object in the world. 
struct BoundingBox {
    float3 minimal;
    float3 maximal;
    /* The type of material the object for this boundng box is made of 

    TYPES: 
    0 = WOOD
    1 = IRON
    2 = CONCRETE
    3 = LEAD
    */
    uint matType;
};

// Information on an object which produces radiation which can distort the image
struct RadiationSource {
    float strength;
    float4 pos;
    uint id;
};

struct Ray{
    float3 origin;
    float3 direction;
};

// Return information about a found ray-AABB intersection
struct AABBIntersectPayload{
    // 0 on a miss and 1 on a hit.
    uint hitOccured;
    float tmin;
    float tmax;
    uint intersectedType;
};



// Input buffers
StructuredBuffer<BoundingBox> sceneGeometry;
uint numGeoObjs; 

StructuredBuffer<RadiationSource> radiationSources;
uint numSources;

// Buffer to write the amount of material between a radiation source and the player
RWStructuredBuffer<float> sourcePlayerBlock;

float3 playerPos;

// Calculate the distance between points a and b in 3D space
float Dist3D(float3 a, float3 b){
    return sqrt(pow(a.x - b.x, 2) + pow(a.y - b.y, 2) + pow(a.z - b.z, 2));
}

// Create a ray cast from a radiation source in the direction of the player 
Ray CastRayToPlayer(RadiationSource castFrom){
    Ray r;
    r.origin = float3(castFrom.pos.xyz);
    r.direction = normalize(playerPos);

    return r;
}

// Check if a ray intersects a bounding box. 
// If it does return the difference between the two intersection points (The amount of material this ray passes through)
// Otherwise return -1.
AABBIntersectPayload RayBoxIntersect(Ray r, BoundingBox b){
    float3 dirfrac;
    // r.dir is unit direction vector of ray
    dirfrac.x = 1.0f / r.direction.x;
    dirfrac.y = 1.0f / r.direction.y;
    dirfrac.z = 1.0f / r.direction.z;

    // Calculate t values
    float t1 = (b.minimal.x - r.origin.x) * dirfrac.x;
    float t2 = (b.maximal.x - r.origin.x) * dirfrac.x;
    float t3 = (b.minimal.y - r.origin.y) * dirfrac.y;
    float t4 = (b.maximal.y - r.origin.y) * dirfrac.y;
    float t5 = (b.minimal.z - r.origin.z) * dirfrac.z;
    float t6 = (b.maximal.z - r.origin.z) * dirfrac.z;

    // Build t min and max
    float tmin = max(max(min(t1, t2), min(t3, t4)), min(t5, t6));
    float tmax = min(min(max(t1, t2), max(t3, t4)), max(t5, t6));

    AABBIntersectPayload pl;
    // Return miss payload if AABB intersection is behind ray (tmax < 0)
    if (tmax < 0)
    {
        pl.hitOccured = 0;
        return pl;
    }

    // Return miss payload if no intersection occurs (tmin > tmax)
    if (tmin > tmax)
    {
        pl.hitOccured = 0;
        return pl;
    }

    // Otherwise return payload for hit
    pl.hitOccured = 1;
    pl.tmax = tmax;
    pl.tmin = tmin;
    pl.intersectedType = b.matType;
    return pl;

}

// Scale the amount of distortion based on the material type
// Scaled value will be between 0 -  1
float MaterialScale(float distortAmt, uint matType){
    if(matType == 0){
        distortAmt /= WOOD_FULL_AMT;
    }
    else if(matType == 1){
        return distortAmt;
    }
    else if(matType == 2){
        distortAmt /= CONCRETE_FULL_AMT;
    }
    else if(matType == 3){
        distortAmt /= LEAD_FULL_AMT;
    }


    return distortAmt;
}


[numthreads(16,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    
    // If out of range end early 
    if(id.x >= numSources){
        return;
    }

    // Check this source against all AABBs and track amount of intersection
    float totalIntersectionAmt = 0;
    Ray r = CastRayToPlayer(radiationSources[id.x]);
    for(uint j=0; j < numGeoObjs; j++ ){
        AABBIntersectPayload intersection = RayBoxIntersect(r, sceneGeometry[j]);
        // If the intersection occurs and is between the player and the source
        float distToPlayer = Dist3D(playerPos, radiationSources[id.x].pos.xyz);
        if(intersection.hitOccured == 1 && distToPlayer > intersection.tmin){
            totalIntersectionAmt += MaterialScale(intersection.tmax - intersection.tmin, 
                intersection.intersectedType);
        }
    }

    // Set this sources material block amount in buffer
    sourcePlayerBlock[id.x] = totalIntersectionAmt;

}
